body file control
{
        #inputs => { "../../lib/commands.cf", "../../lib/paths.cf" };
        namespace => "linux_kernel_inventory";
}
bundle agent kernel_params
{

  vars:
    linux::
      "kernel_param_sourcefile"
        string => "/proc/cmdline";

      "kernel_param_data"
        data => data_readstringarrayidx($(kernel_param_sourcefile), "", "\s+", 1, 0);

      "kernel_params"
        handle => "inventory_kernel_params_kernel_params",
        slist => getvalues("kernel_param_data[0]");

      "c_kernel_params_map[$(kernel_params)]"
        string => canonify("$(kernel_params)");

      "$(c_kernel_params_map[$(kernel_params)])"
        string => "$(kernel_params)",
        meta => {
                  "inventory",
                  "attribute_name=Kernel Parameters",
                  "derived_from=$(kernel_param_sourcefile)"
                };

  reports:
    !linux.(DEBUG|DEBUG_linux_kernel|DEBUG_linux_kernel_params_inventory)::
      "$(const.t)SKIPPED: I don't know how to inventory kernel params on non linux hosts";
}

body file control
{
        namespace => "default";
}
bundle agent __main__
{
      methods: "linux_kernel_inventory:kernel_params";
}
